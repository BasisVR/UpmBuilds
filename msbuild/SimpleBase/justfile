git_repo := "https://github.com/ssg/SimpleBase.git"
git_ref := "4.0.2"
dll_name := "SimpleBase.dll"
lib_name := "SimpleBase"
out_dir := invocation_directory() / "build"

working_dir := `mktemp -d`
repo_dir := working_dir / "repo"
dotnet_artifact_dir := working_dir / "dotnet_artifacts"
upm_contents_dir := working_dir / "upm_contents"

default: assemble_upm

# Clean build outputs
clean:
    rm -rf {{working_dir}}

clone:
    git clone --single-branch --branch {{git_ref}} {{git_repo}} {{repo_dir}}

restore:
    dotnet restore {{repo_dir}} --artifacts-path {{dotnet_artifact_dir}}

build:
    dotnet build --configuration Release {{repo_dir}} -o {{dotnet_artifact_dir}}

assemble_upm: clone restore build && clean
    mkdir -p {{upm_contents_dir}}
    cp {{dotnet_artifact_dir / dll_name}} {{upm_contents_dir}}
    cp package.json {{upm_contents_dir}}
    mkdir -p {{out_dir}}
    tar -cvf {{out_dir / lib_name}}.tgz -C {{upm_contents_dir}} .
